/-
Copyright (c) 2024 Amazon.com, Inc. or its affiliates. All Rights Reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author(s): Siddharth Bhat

In this file, we define proof automation for separation conditions of memory.

References:
- https://github.com/leanprover/lean4/blob/240ebff549a2cf557f9abe9568f5de885f13e50d/src/Lean/Elab/Tactic/Omega/OmegaM.lean
- https://github.com/leanprover/lean4/blob/240ebff549a2cf557f9abe9568f5de885f13e50d/src/Lean/Elab/Tactic/Omega/Frontend.lean
-/
import Arm
import Arm.Memory.MemoryProofs
import Arm.BitVec
import Arm.Memory.Attr
import Arm.Memory.AddressNormalization
import Lean
import Lean.Meta.Tactic.Rewrite
import Lean.Meta.Tactic.Rewrites
import Lean.Elab.Tactic.Conv
import Lean.Elab.Tactic.Conv.Basic
import Tactics.Simp
import Lean.Elab.Tactic.Omega
import Arm.Memory.AddressNormalization
import Arm.Memory.NoOverflow
open Lean Meta Elab Tactic


/-! ## Memory Separation Automation

##### A Note on Notation

- `[a..an)`: a range of memory starting with base address `a` of length `an`.
  aka `mem_legal' a an`.
- `[a..an) ‚üÇ [b..bn)`: `mem_disjoint' a an b bn`.
- `[a..an] ‚äÜ [b..bn)`: `mem_subset' a an b bn`

##### Tactic Loop

The core tactic tries to simplify expressions of the form:
`mem.write_bytes [b..bn) val |>. read_bytes [a..an)`
by case splitting:

1. If `[a..an) ‚üÇ [b..bn)`, the write does not alias the read,
  and can be replaced with ` mem.read_bytes [a..an) `
2. If `[a..an] ‚äÜ [b..bn)`, the write aliases the read, and can be replaced with
  `val.extractLsBs adjust([a..an), [b..bn))`. Here, `adjust` is a function that
  adjusts the read indices `[a..an)` with respect to the write indices `[b..bn)`,
  to convert a read from `mem` into a read from `val`.

The tactic shall be implemented as follows:
1. Search the goal state for `mem.write_bytes [b..bn) val |>.read_bytes [a..an)`.
2. Try to prove that either `[a..an) ‚üÇ [b..bn)`, or `[a..an) ‚äÜ [b..bn)`.
    2a. First search the local context for assumptions of this type.
    2b. Try to deduce `[a..an) ‚üÇ [b..bn)` from the fact that
        subsets of disjoint sets are disjoint.
        So try to find `[a'..an')`, `[b'...bn')` such that:
          (i) `[a..an) ‚äÜ [a'..an')`.
          (ii) `[b..bn) ‚äÜ [b'..bn')`.
          (iii) and `[a'..an') ‚üÇ [b'...bn')`.
    2b. Try to deduce `[a..an) ‚äÜ [b..bn)` from transitivity of subset.
        So try to find `[c..cn)` such that:
        (i) `[a..an) ‚äÜ [c..cn)`
        (ii) `[c..cn) ‚äÜ [b..bn)`
    2c. If this also fails, then reduce all hypotheses to
        linear integer arithmetic, and try to invoke `omega` to prove either
        `[a..an) ‚üÇ [b..bn)` or `[a..an) ‚äÜ [b..bn)`.
3. Given a proof of either `[a..an) ‚üÇ [b..bn)` or `[a..an) ‚äÜ [b..bn)`,
  simplify using the appropriate lemma from `Mem/Separate.lean`.
4. If we manage to prove *both* `[a..an) ‚üÇ [b..bn)` *and* `[a..an) ‚äÜ [b..bn)`,
   declare victory as this is a contradiction. This may look useless,
   but feels like it maybe useful to prove certain memory states as impossible.

##### Usability

- If no mem separate/subset assumptions are present,
  then throw an error to tell the user that we expect them to
  specify such assumptions for all memory regions of interest.
  LNSym doesn't support automated verification of programs that
  do dynamic memory allocation.

-  If any non-primed separate/subset assumptions are detected,
  error out to tell the user that no automation is supported in this case.

##### Future Work

- Add support for disjoint constraints amongst $n$ memory regions.
  This will perform proof search for `List.pairwise ‚üÇ mems`.
- Create a new concept, `MemRegion`, which we currently call `MemSpan`.
-/

section BvOmega

/- We tag `mem_legal'` as `bv_toNat` here, since we want to actually lazily unfold this.
Doing it here lets us remove it from `bv_toNat` simp-set as a change to `SeparateAutomation.lean`,
without needing us to modify the core definitions file which incurs a recompile cost,
making experimentation annoying.
-/
attribute [bv_toNat] mem_legal'

end BvOmega

namespace SeparateAutomation

structure SimpMemConfig where
  /-- number of times rewrites must be performed. -/
  rewriteFuel : Nat := 1000
  /-- whether an error should be thrown if the tactic makes no progress. -/
  failIfUnchanged : Bool := true
  /-- whether `simp_mem` should always try to use `omega` to close the goal,
    even if goal state is not recognized as one of the blessed states.
    This is useful when one is trying to establish some numerical invariant
    about addresses based on knowledge of memory.
    e.g.
    ```
    h : mem_separate' a 10 b 10
    hab : a < b
    ‚ä¢ a + 5 < b
    ```
  -/
  useOmegaToClose : Bool := false

/-- Context for the `SimpMemM` monad, containing the user configurable options. -/
structure Context where
  /-- User configurable options for `simp_mem`. -/
  cfg : SimpMemConfig
  /-- Cache of `bv_toNat` simp context. -/
  bvToNatSimpCtx : Simp.Context
  /-- Cache of `bv_toNat` simprocs. -/
  bvToNatSimprocs : Array Simp.Simprocs

def Context.init (cfg : SimpMemConfig) : MetaM Context := do
  let (bvToNatSimpCtx, bvToNatSimprocs) ‚Üê
    LNSymSimpContext
      -- | TODO/FIXME: bv_decide does not like it when the equation is fully ground.
      --  So we work around it by setting `failIfUnchanged := false`.
      (config := {failIfUnchanged := false, ground := true})
      -- (simp_attrs := #[`bv_toNat, `address_normalization]) -- too slow, times out on memcpy.
      (simp_attrs := #[`memory_defs_bv])
      (useDefaultSimprocs := false)
  return {cfg, bvToNatSimpCtx, bvToNatSimprocs}

/-- a Proof of `e : Œ±`, where `Œ±` is a type such as `MemLegalProp`. -/
structure Proof (Œ± : Type) (e : Œ±) where
  /-- `h` is an expression of type `e`. -/
  h : Expr

def WithProof.e {Œ± : Type} {e : Œ±} (_p : Proof Œ± e) : Œ± := e

instance [ToMessageData Œ±] : ToMessageData (Proof Œ± e) where
  toMessageData proof := m! "{proof.h}: {e}"

/--
We use `BitVec 64` when we define sizes in `mem_legal'`, `mem_subset'`, and `mem_separate'`,
since this helps `bv_decide` automatically decide such goals.

However, for `Memory.read_bytes / Memory.write_bytes`, we use `Nat` since the number of bytes
to read is a natural number that we perform induction on.
We can write a wrapper that uses bitvectors as well.

For now, we choose to write a wrapper that knows how to convert between `Nat` and `BitVec 64`.

If this turns out to be too complex, we should write a wrapper around `Memory.read_bytes`
and `Memory.write_bytes` that uses `BitVec 64` instead of `Nat`.
-/
structure ExprNatOrBv where
  expr : Expr
  isNat : Bool -- whether the expr is a nat.
deriving Inhabited, BEq, Hashable

instance : ToMessageData ExprNatOrBv where
  toMessageData e := m!"{e.expr} : {if e.isNat then "Nat" else "BitVec 64"}"

def ExprNatOrBv.asNat (e : ExprNatOrBv) : Expr :=
  if e.isNat then
    e.expr
  else
    mkAppN (mkConst ``BitVec.toNat) #[mkNatLit 64, e.expr]

def ExprNatOrBv.asBV (e : ExprNatOrBv) : Expr :=
  if e.isNat then
    mkAppN (mkConst ``BitVec.ofNat) #[mkNatLit 64, e.expr]
  else
    e.expr

/-- make a NatOrBv with a known natural number. performs no checking. -/
def ExprNatOrBv.ofNat! (e : Expr) : ExprNatOrBv :=
    { expr := e, isNat := true }

/-- make a NatOrBv with a known bitvectr. performs no checking. -/
def ExprNatOrBv.ofBV! (e : Expr) : ExprNatOrBv :=
    { expr := e, isNat := false }

def ExprNatOrBv.ofExpr? (e : Expr) : MetaM (Option ExprNatOrBv) := do
  let ety ‚Üê inferType e
  if ety == (mkConst ``Nat) then
    return .some { expr := e, isNat := true }
  else
    let f := e.getAppFn
    if f == mkConst ``BitVec then
      return .some { expr := e, isNat := false }
    else
      return none

structure MemSpanExpr where
  base : Expr
  n : ExprNatOrBv -- here, in Memory read/write, we have `n : Nat`, but in `mem_separate'`, we have `n : BitVec 64`.
deriving Inhabited

/-- info: Memory.Region.mk (a n : BitVec 64) : Memory.Region -/
#guard_msgs in #check Memory.Region.mk

def MemSpanExpr.toExpr (span : MemSpanExpr) : Expr :=
  mkAppN (Expr.const ``Memory.Region.mk []) #[span.base, span.n.asBV]

def MemSpanExpr.toTypeExpr  : Expr :=
    (Expr.const ``Memory.Region [])

instance : ToMessageData MemSpanExpr where
  toMessageData span := m! "[{span.base}..{span.n})"

/-- info: mem_legal' (a n : BitVec 64) : Prop -/
#guard_msgs in #check mem_legal'

/-- a term of the form `mem_legal' a` -/
structure MemLegalProp  where
  span : MemSpanExpr

/-- convert this back to an Expr -/
def MemLegalProp.toExpr (legal : MemLegalProp) : Expr :=
  mkAppN (Expr.const ``mem_legal' []) #[legal.span.base, legal.span.n.asBV]

instance : ToMessageData MemLegalProp where
  toMessageData e := m!"{e.span}.legal"

/-- Coerce a span into a `span.legal` hypothesis. -/
instance : Coe MemSpanExpr MemLegalProp where
  coe := MemLegalProp.mk

/-- info: mem_subset' (a an b bn : BitVec 64) : Prop -/
#guard_msgs in #check mem_subset'

/-- a proposition `mem_subset' a an b bn`. -/
structure MemSubsetProp where
  sa : MemSpanExpr
  sb : MemSpanExpr

instance : ToMessageData MemSubsetProp where
  toMessageData e := m!"{e.sa}‚äÜ{e.sb}"

abbrev MemSubsetProof := Proof MemSubsetProp

def MemSubsetProof.mk {e : MemSubsetProp} (h : Expr) : MemSubsetProof e :=
  { h }

/-- info: mem_separate' (a an b bn : BitVec 64) : Prop -/
#guard_msgs in #check mem_separate'

/-- A proposition `mem_separate' a an b bn`. -/
structure MemSeparateProp where
  sa : MemSpanExpr
  sb : MemSpanExpr

instance : ToMessageData MemSeparateProp where
  toMessageData e := m!"{e.sa}‚üÇ{e.sb}"

abbrev MemSeparateProof := Proof MemSeparateProp

def MemSeparateProof.mk {e : MemSeparateProp} (h : Expr) : MemSeparateProof e :=
  { h }

abbrev MemLegalProof := Proof MemLegalProp

def MemLegalProof.mk {e : MemLegalProp} (h : Expr) : MemLegalProof e :=
  { h }


/-- info: Memory.Region.pairwiseSeparate (mems : List Memory.Region) : Prop -/
#guard_msgs in #check Memory.Region.pairwiseSeparate

/--
A proposition `Memory.Region.pairwiseSeparate [x1, x2, ..., xn]`.
-/
structure MemPairwiseSeparateProp where
  xs : Array MemSpanExpr

/-- info: List.nil.{u} {Œ± : Type u} : List Œ± -/
#guard_msgs in #check List.nil

/-- info: List.cons.{u} {Œ± : Type u} (head : Œ±) (tail : List Œ±) : List Œ± -/
#guard_msgs in #check List.cons

/-- Given `Memory.Region.pairwiseSeparate [x1, ..., xn]`,
get the expression corresponding `[x1, ..., xn]`. -/
def MemPairwiseSeparateProp.getMemSpanListExpr
    (e : MemPairwiseSeparateProp) : Expr := Id.run do
  let memoryRegionTy : Expr := mkConst ``Memory.Region
  let mut out := mkApp (mkConst  ``List.nil [0]) memoryRegionTy
  for i in [0:e.xs.size] do
    let x := e.xs[e.xs.size - i - 1]!
    out := mkAppN (mkConst ``List.cons [0]) #[memoryRegionTy, x.toExpr, out]
  return out

/-- Get the expression `Memory.Region.pairwiseSeparate [x1, ..., xn]` -/
def MemPairwiseSeparateProp.toExpr (e : MemPairwiseSeparateProp) : Expr :=
  mkApp (mkConst ``Memory.Region.pairwiseSeparate) e.getMemSpanListExpr

instance : ToMessageData MemPairwiseSeparateProp where
  toMessageData e := m!"pairwiseSeparate {e.xs.toList}"

abbrev MemPairwiseSeparateProof := Proof MemPairwiseSeparateProp

/-- info: Memory.read_bytes (n : Nat) (addr : BitVec 64) (m : Memory) : BitVec (n * 8) -/
#guard_msgs in #check Memory.read_bytes

/-- an occurrence of `Memory.read_bytes`. -/
structure ReadBytesExpr where
  span : MemSpanExpr
  mem : Expr

/-- match an expression `e` to `Memory.read_bytes`. -/
def ReadBytesExpr.ofExpr? (e : Expr) : Option (ReadBytesExpr) :=
  match_expr e with
  | Memory.read_bytes n addr m =>
    some { span := { base := addr, n := ExprNatOrBv.ofNat! n }, mem := m }
  | _ => none

-- TODO: try to use `pp.deepTerms` to make the memory expressions more readable.
instance : ToMessageData ReadBytesExpr where
  toMessageData e := m!"{e.mem}[{e.span}]"

/--
info: Memory.write_bytes (n : Nat) (addr : BitVec 64) (val : BitVec (n * 8)) (m : Memory) : Memory
-/
#guard_msgs in #check Memory.write_bytes

structure WriteBytesExpr where
  span : MemSpanExpr
  val : Expr
  mem : Expr

instance : ToMessageData WriteBytesExpr where
  toMessageData e := m!"{e.mem}[{e.span} := {e.val}]"

def WriteBytesExpr.ofExpr? (e : Expr) : Option WriteBytesExpr :=
  match_expr e with
  | Memory.write_bytes n addr val m =>
    some { span := { base := addr, n := ExprNatOrBv.ofNat! n }, val := val, mem := m }
  | _ => none


/--
A proof of the form `h : val = Mem.read_bytes ...`.
Note that we expect the canonical ordering of `val` on the left hand side.
If `val` was on the right hand, we build `h` wih an `Eq.symm` to
enforce this canonical form.

TODO: there must be a better way to handle this?
 -/
structure ReadBytesEqProof  where
  val : Expr
  read : ReadBytesExpr
  h : Expr

instance : ToMessageData ReadBytesEqProof where
  toMessageData proof := m!"{proof.h} : {proof.val} = {proof.h}"

/--
we can have some kind of funny situation where both LHS and RHS are ReadBytes.
For example, `mem1.read base1 n = mem2.read base2 n`.
In such a scenario, we should record both reads.
-/
def ReadBytesEqProof.ofExpr? (eval : Expr) (etype : Expr) :  Array ReadBytesEqProof := Id.run do
  let mut out := #[]
  if let .some ‚ü®_ty, lhs, rhs‚ü© := etype.eq? then do
    let lhs := lhs
    let rhs := rhs
    if let .some read := ReadBytesExpr.ofExpr? lhs then
      out := out.push { val := rhs, read := read, h := eval }

    if let .some read := ReadBytesExpr.ofExpr? rhs then
      out:= out.push { val := lhs, read := read, h := eval }
  return out

inductive Hypothesis
| separate (proof : MemSeparateProof e)
| subset (proof : MemSubsetProof e)
| legal (proof : MemLegalProof e)
| pairwiseSeparate (proof : MemPairwiseSeparateProof e)
| read_eq (proof : ReadBytesEqProof)

def Hypothesis.proof : Hypothesis ‚Üí Expr
| .pairwiseSeparate proof  => proof.h
| .separate proof => proof.h
| .subset proof => proof.h
| .legal proof => proof.h
| .read_eq proof => proof.h

instance : ToMessageData Hypothesis where
  toMessageData
  | .subset proof => toMessageData proof
  | .separate proof => toMessageData proof
  | .legal proof => toMessageData proof
  | .read_eq proof => toMessageData proof
  | .pairwiseSeparate proof => toMessageData proof

/-- The internal state for the `SimpMemM` monad, recording previously encountered atoms. -/
structure State where
  hypotheses : Array Hypothesis := #[]
  rewriteFuel : Nat

def State.init (cfg : SimpMemConfig) : State :=
  { rewriteFuel := cfg.rewriteFuel}

abbrev SimpMemM := StateRefT State (ReaderT Context TacticM)

def SimpMemM.run (m : SimpMemM Œ±) (cfg : SimpMemConfig) : TacticM Œ± := do
  m.run' (State.init cfg) |>.run (‚Üê Context.init cfg)

/-- Add a `Hypothesis` to our hypothesis cache. -/
def SimpMemM.addHypothesis (h : Hypothesis) : SimpMemM Unit :=
  modify fun s => { s with hypotheses := s.hypotheses.push h }

def SimpMemM.withMainContext (ma : SimpMemM Œ±) : SimpMemM Œ± := do
  (‚Üê getMainGoal).withContext ma

def SimpMemM.withContext (g : MVarId) (ma : SimpMemM Œ±) : SimpMemM Œ± := do
  g.withContext ma

/-- create a trace node in trace class (i.e. `set_option traceClass true`),
with header `header`, whose default collapsed state is `collapsed`. -/
def SimpMemM.withTraceNode (header : MessageData) (k : SimpMemM Œ±)
    (collapsed : Bool := true)
    (traceClass : Name := `simp_mem.info) : SimpMemM Œ± :=
  Lean.withTraceNode traceClass (fun _ => return header) k (collapsed := collapsed)

/-- Get the cached simp context for bv_toNat -/
def SimpMemM.getBvToNatSimpCtx : SimpMemM Simp.Context := do
  return (‚Üê read).bvToNatSimpCtx

/-- Get the cached simpprocs for bv_toNat -/
def SimpMemM.getBvToNatSimprocs : SimpMemM (Array Simp.Simprocs) := do
  return (‚Üê read).bvToNatSimprocs

def processingEmoji : String := "‚öôÔ∏è"

def consumeRewriteFuel : SimpMemM Unit :=
  modify fun s => { s with rewriteFuel := s.rewriteFuel - 1 }

def outofRewriteFuel? : SimpMemM Bool := do
  return (‚Üê get).rewriteFuel == 0

/-- Create a trace note that folds `header` with `(NOTE: can be large)`,
and prints `msg` under such a trace node.
-/
def SimpMemM.traceLargeMsg (header : MessageData) (msg : MessageData) : SimpMemM Unit :=
    withTraceNode m!"{header} (NOTE: can be large)" do
      trace[simp_mem.info] msg


def getConfig : SimpMemM SimpMemConfig := do
  let ctx ‚Üê read
  return ctx.cfg

/-- info: state_value (fld : StateField) : Type -/
#guard_msgs in #check state_value

/-
Introduce a new definition into the local context, simplify it using `simp`,
and return the FVarId of the new definition in the goal.
-/
def simpAndIntroDef (name : String) (hdefVal : Expr) : SimpMemM FVarId  := do
    -- TODO(@bollu): disable this eventually
    trace[simp_mem] "DEBUG CODE: checking that '{hdefVal}' is type correct."
    check hdefVal
    let name ‚Üê mkFreshUserName <| .mkSimple name
    let goal ‚Üê getMainGoal
    let hdefTy ‚Üê inferType hdefVal

    /- Simp to gain some more juice out of the defn.. -/
    let mut simpTheorems : Array SimpTheorems := #[]
    for a in #[`minimal_theory, `bitvec_rules] do
      let some ext ‚Üê (getSimpExtension? a)
        | throwError m!"[simp_mem] Internal error: simp attribute {a} not found!"
      simpTheorems := simpTheorems.push (‚Üê ext.getTheorems)

    -- unfold `state_value.
    simpTheorems := simpTheorems.push <| ‚Üê ({} : SimpTheorems).addDeclToUnfold `state_value
    let simpCtx : Simp.Context := {
      simpTheorems,
      config := { decide := true, failIfUnchanged := false },
      congrTheorems := (‚Üê Meta.getSimpCongrTheorems)
    }
    let (simpResult, _stats) ‚Üê simp hdefTy simpCtx (simprocs := #[])
    let hdefVal ‚Üê simpResult.mkCast hdefVal
    let hdefTy ‚Üê inferType hdefVal

    let goal ‚Üê goal.assert name hdefTy hdefVal
    let (fvar, goal) ‚Üê goal.intro1P
    replaceMainGoal [goal]
    return fvar

section Hypotheses

/--
info: mem_separate'.ha {a an b bn : BitVec 64} (self : mem_separate' a an b bn) : mem_legal' a an
-/
#guard_msgs in #check mem_separate'.ha

def MemSeparateProof.mem_separate'_ha (self : MemSeparateProof sep) :
    MemLegalProof sep.sa :=
  let h := mkAppN (Expr.const ``mem_separate'.ha []) #[sep.sa.base, sep.sa.n.asBV, sep.sb.base, sep.sb.n.asBV, self.h]
  MemLegalProof.mk h

/--
info: mem_separate'.hb {a an b bn : BitVec 64} (self : mem_separate' a an b bn) : mem_legal' b bn
-/
#guard_msgs in #check mem_separate'.hb

def MemSeparateProof.mem_separate'_hb (self : MemSeparateProof sep) :
    MemLegalProof sep.sb :=
  let h := mkAppN (Expr.const ``mem_separate'.hb []) #[sep.sa.base, sep.sa.n.asBV, sep.sb.base, sep.sb.n.asBV, self.h]
  MemLegalProof.mk h

/-- info: mem_subset'.ha {a an b bn : BitVec 64} (self : mem_subset' a an b bn) : mem_legal' a an -/
#guard_msgs in #check mem_subset'.ha

def MemSubsetProof.mem_subset'_ha (self : MemSubsetProof sub) :
    MemLegalProof sub.sa :=
  let h := mkAppN (Expr.const ``mem_subset'.ha [])
    #[sub.sa.base, sub.sa.n.asBV, sub.sb.base, sub.sb.n.asBV, self.h]
  MemLegalProof.mk h

/-- info: mem_subset'.hb {a an b bn : BitVec 64} (self : mem_subset' a an b bn) : mem_legal' b bn -/
#guard_msgs in #check mem_subset'.hb

def MemSubsetProof.mem_subset'_hb (self : MemSubsetProof sub) :
    MemLegalProof sub.sb :=
  let h := mkAppN (Expr.const ``mem_subset'.hb [])
      #[sub.sa.base, sub.sa.n.asBV, sub.sb.base, sub.sb.n.asBV, self.h]
  MemLegalProof.mk h

/-- match an expression `e` to a `mem_legal'`. -/
def MemLegalProp.ofExpr? (e : Expr) : Option (MemLegalProp) :=
  match_expr e with
  | mem_legal' a n => .some { span := { base := a, n := ExprNatOrBv.ofBV! n } }
  | _ => none

/-- match an expression `e` to a `mem_subset'`. -/
def MemSubsetProp.ofExpr? (e : Expr) : Option (MemSubsetProp) :=
  match_expr e with
  | mem_subset' a na b nb =>
    let sa : MemSpanExpr := { base := a, n := ExprNatOrBv.ofBV! na }
    let sb : MemSpanExpr := { base := b, n := ExprNatOrBv.ofBV! nb }
    .some { sa, sb }
  | _ => none

/-- match an expression `e` to a `mem_separate'`. -/
def MemSeparateProp.ofExpr? (e : Expr) : Option MemSeparateProp :=
  match_expr e with
  | mem_separate' a na b nb =>
    let sa : MemSpanExpr := ‚ü®a, ExprNatOrBv.ofBV! na‚ü©
    let sb : MemSpanExpr := ‚ü®b, ExprNatOrBv.ofBV! nb‚ü©
    .some { sa, sb }
  | _ => none

/-- info: Prod.mk.{u, v} {Œ± : Type u} {Œ≤ : Type v} (fst : Œ±) (snd : Œ≤) : Œ± √ó Œ≤ -/
#guard_msgs in #check Prod.mk

/-- info: List.cons.{u} {Œ± : Type u} (head : Œ±) (tail : List Œ±) : List Œ± -/
#guard_msgs in #check List.cons

/-- info: List.nil.{u} {Œ± : Type u} : List Œ± -/
#guard_msgs in #check List.nil

/-- match an expression `e` to a `Memory.Region.pairwiseSeparate`. -/
partial def MemPairwiseSeparateProof.ofExpr? (e : Expr) : Option MemPairwiseSeparateProp :=
  match_expr e with
  | Memory.Region.pairwiseSeparate xs => do
      let .some xs := go xs #[] | none
      some { xs := xs }
  | _ => none
  where
    go (e : Expr) (xs : Array MemSpanExpr) : Option (Array MemSpanExpr) :=
      match_expr e with
      | List.cons _Œ± ex exs =>
        match_expr ex with
        | Prod.mk _ta _tb a n =>
          let x : MemSpanExpr := ‚ü®a, ExprNatOrBv.ofBV! n‚ü©
          go exs (xs.push x)
        | _ => none
      | List.nil _Œ± => some xs
      | _ => none


/-- Match an expression `h` to see if it's a useful hypothesis. -/
def hypothesisOfExpr (h : Expr) (hyps : Array Hypothesis) : MetaM (Array Hypothesis) := do
  let ht ‚Üê inferType h
  trace[simp_mem.info] "{processingEmoji} Processing '{h}' : '{toString ht}'"
  if let .some sep := MemSeparateProp.ofExpr? ht then
    let proof : MemSeparateProof sep := ‚ü®h‚ü©
    let hyps := hyps.push (.separate proof)
    let hyps := hyps.push (.legal proof.mem_separate'_ha)
    let hyps := hyps.push (.legal proof.mem_separate'_hb)
    return hyps
  else if let .some sub := MemSubsetProp.ofExpr? ht then
    let proof : MemSubsetProof sub := ‚ü®h‚ü©
    let hyps := hyps.push (.subset proof)
    let hyps := hyps.push (.legal proof.mem_subset'_ha)
    let hyps := hyps.push (.legal proof.mem_subset'_hb)
    return hyps
  else if let .some legal := MemLegalProp.ofExpr? ht then
    let proof : MemLegalProof legal := ‚ü®h‚ü©
    let hyps := hyps.push (.legal proof)
    return hyps
  else if let .some pairwiseSep := MemPairwiseSeparateProof.ofExpr? ht then
    let proof : MemPairwiseSeparateProof pairwiseSep := ‚ü®h‚ü©
    let hyps := hyps.push (.pairwiseSeparate proof)
    return hyps
  else
    let mut hyps := hyps
    for eqProof in ReadBytesEqProof.ofExpr? h ht do
      let proof : Hypothesis := .read_eq eqProof
      hyps := hyps.push proof
    return hyps

/-- info: mem_legal'.bv_def (a n : BitVec 64) (h : mem_legal' a n) : a ‚â§ a + n -/
#guard_msgs in #check mem_legal'.bv_def


/-- Build a term corresponding to `mem_legal'.bv_def`. -/
def MemLegalProof.bv_def (h : MemLegalProof e) : Expr :=
  mkAppN (Expr.const ``mem_legal'.bv_def []) #[e.span.base, e.span.n.asBV, h.h]

/-- Add the omega fact from `mem_legal'.def`. -/
def MemLegalProof.addSolverFacts (h : MemLegalProof e) (g : MVarId) (args : Array Expr) :
    SimpMemM (Array Expr) := do
  SimpMemM.withContext g do
    let fvar ‚Üê simpAndIntroDef "hmemLegal_bv" h.bv_def
    trace[simp_mem.info]  "{h}: added omega fact ({h.bv_def})"
    return args.push (Expr.fvar fvar)

/--
info: mem_subset'.bv_def (a an b bn : BitVec 64) (h : mem_subset' a an b bn) :
  mem_legal' a an ‚àß mem_legal' b bn ‚àß b ‚â§ a ‚àß a + an ‚â§ b + bn
-/
#guard_msgs in #check mem_subset'.bv_def

/--
Build a term corresponding to `mem_subset'.bv_def` which has facts written
in a style that is exploitable by omega.
-/
def MemSubsetProof.bv_def (h : MemSubsetProof e) : Expr :=
  mkAppN (Expr.const ``mem_subset'.bv_def [])
    #[e.sa.base, e.sa.n.asBV, e.sb.base, e.sb.n.asBV, h.h]

/-- Add the omega fact from `mem_legal'.bv_def` into the main goal. -/
def MemSubsetProof.addSolverFacts (h : MemSubsetProof e) (g : MVarId) (args : Array Expr) :
    SimpMemM (Array Expr) := do
  SimpMemM.withContext g do
    let fvar ‚Üê simpAndIntroDef "hmemSubset_omega" h.bv_def
    trace[simp_mem.info]  "{h}: added omega fact ({h.bv_def})"
    return args.push (Expr.fvar fvar)

/--
info: mem_separate'.bv_def (a an b bn : BitVec 64) (h : mem_separate' a an b bn) :
  mem_legal' a an ‚àß mem_legal' b bn ‚àß (a + an ‚â§ b ‚à® a ‚â• b + bn ‚à® an = 0 ‚à® bn = 0)
-/
#guard_msgs in #check mem_separate'.bv_def

/--
Build a term corresponding to `mem_separate'.bv_def` which has facts written
in a style that is exploitable by omega.
-/
def MemSeparateProof.bv_def (h : MemSeparateProof e) : Expr :=
  mkAppN (Expr.const ``mem_separate'.bv_def [])
    #[e.sa.base, e.sa.n.asBV, e.sb.base, e.sb.n.asBV, h.h]

/-- Add the omega fact from `mem_legal'.bv_def`. -/
def MemSeparateProof.addSolverFacts (h : MemSeparateProof e) (g : MVarId) (args : Array Expr) :
    SimpMemM (Array Expr) := do
  SimpMemM.withContext g do
    -- simp only [bitvec_rules] (failIfUnchanged := false)
    let fvar ‚Üê simpAndIntroDef "hmemSeparate_omega" h.bv_def
    trace[simp_mem.info]  "{h}: added omega fact ({h.bv_def})"
    return args.push (Expr.fvar fvar)



/-- info: Ne.{u} {Œ± : Sort u} (a b : Œ±) : Prop -/
#guard_msgs in #check Ne

/-- info: List.get?.{u} {Œ± : Type u} (as : List Œ±) (i : Nat) : Option Œ± -/
#guard_msgs in #check List.get?

/-- Make the expression `mems.get? i = some a`. -/
def mkListGetEqSomeTy (mems : MemPairwiseSeparateProp) (i : Nat) (a : MemSpanExpr) : SimpMemM Expr := do
  let lhs ‚Üê mkAppOptM ``List.get? #[.none, mems.getMemSpanListExpr, mkNatLit i]
  let rhs ‚Üê mkSome MemSpanExpr.toTypeExpr a.toExpr
  mkEq lhs rhs

/--
info: Memory.Region.separate'_of_pairwiseSeprate_of_mem_of_mem {mems : List Memory.Region}
  (h : Memory.Region.pairwiseSeparate mems) (i j : Nat) (hij : i ‚â† j) (a b : Memory.Region) (ha : mems.get? i = some a)
  (hb : mems.get? j = some b) : mem_separate' a.fst a.snd b.fst b.snd
-/
#guard_msgs in #check Memory.Region.separate'_of_pairwiseSeprate_of_mem_of_mem

/-- make `Memory.Region.separate'_of_pairwiseSeprate_of_mem_of_mem i j (by decide) a b rfl rfl`. -/
def MemPairwiseSeparateProof.mem_separate'_of_pairwiseSeparate_of_mem_of_mem
    (self : MemPairwiseSeparateProof mems) (i j : Nat) (a b : MemSpanExpr)  :
    SimpMemM <| MemSeparateProof ‚ü®a, b‚ü© := do
  let iexpr := mkNatLit i
  let jexpr := mkNatLit j
    -- i ‚â† j
  let hijTy := mkAppN (mkConst ``Ne [1]) #[(mkConst ``Nat), mkNatLit i, mkNatLit j]
  -- mems.get? i = some a
  let hijVal ‚Üê mkDecideProof hijTy
  let haVal ‚Üê mkEqRefl <| ‚Üê mkSome MemSpanExpr.toTypeExpr a.toExpr
  let hbVal ‚Üê mkEqRefl <| ‚Üê mkSome MemSpanExpr.toTypeExpr b.toExpr
  let h := mkAppN (Expr.const ``Memory.Region.separate'_of_pairwiseSeprate_of_mem_of_mem [])
    #[mems.getMemSpanListExpr,
      self.h,
      iexpr,
      jexpr,
      hijVal,
      a.toExpr,
      b.toExpr,
      haVal,
      hbVal]
  return ‚ü®h‚ü©
/--
Currently, if the list is syntacticaly of the form [x1, ..., xn],
 we create hypotheses of the form `mem_separate' xi xj` for all i, j..
This can be generalized to pairwise separation given hypotheses x ‚àà xs, x' ‚àà xs.
-/
def MemPairwiseSeparateProof.addSolverFacts (h : MemPairwiseSeparateProof e) (g : MVarId) (args : Array Expr) :
    SimpMemM (Array Expr) := do
  -- We need to loop over i, j where i < j and extract hypotheses.
  -- We need to find the length of the list, and return an `Array MemRegion`.
  let mut args := args
  for i in [0:e.xs.size] do
    for j in [i+1:e.xs.size] do
      let a := e.xs[i]!
      let b := e.xs[j]!
      args ‚Üê SimpMemM.withTraceNode m!"Exploiting ({i}, {j}) : {a} ‚üÇ {b}" do
        let proof ‚Üê h.mem_separate'_of_pairwiseSeparate_of_mem_of_mem i j a b
        SimpMemM.traceLargeMsg m!"added {‚Üê inferType proof.h}" m!"{proof.h}"
        proof.addSolverFacts g args
  return args
/--
Given a hypothesis, add declarations that would be useful for omega-blasting
-/
def Hypothesis.addSolverFactsOfHyp (g : MVarId) (h : Hypothesis) (args : Array Expr) : SimpMemM (Array Expr) :=
  match h with
  -- | Hypothesis.legal h => h.addSolverFacts g args
  -- | Hypothesis.subset h => h.addSolverFacts g args
  -- | Hypothesis.separate h => h.addSolverFacts g args
  | Hypothesis.pairwiseSeparate h => h.addSolverFacts g args
  -- | Hypothesis.read_eq _h => return args -- read has no extra `omega` facts.
  | _ => return args
/--
Accumulate all omega defs in `args`.
-/
def Hypothesis.addSolverFactsOfHyps (g : MVarId) (hs : List Hypothesis) (args : Array Expr)
    : SimpMemM (Array Expr) := do
  SimpMemM.withTraceNode m!"Adding omega facts from hypotheses" do
    let mut args := args
    for h in hs do
      args ‚Üê h.addSolverFactsOfHyp g args
    return args

end Hypotheses

section ReductionToOmega

/--
Given a `a : Œ±` (for example, `(a = legal) : (Œ± = mem_legal)`),
produce an `Expr` whose type is `<omega fact> ‚Üí Œ±`.
For example, `mem_legal.of_bv` is a function of type:
  `(h : a.toNat + n ‚â§ 2 ^ 64) ‚Üí mem_legal a n`.
-/
class SolverReducible (Œ± : Type) where
  reduceToSolver : Œ± ‚Üí Expr

/-- info: mem_legal'.of_bv (a n : BitVec 64) (h : a ‚â§ a + n) : mem_legal' a n -/
#guard_msgs in #check mem_legal'.of_bv

instance : SolverReducible MemLegalProp where
  reduceToSolver legal :=
    let a := legal.span.base
    let n := legal.span.n
    mkAppN (Expr.const ``mem_legal'.of_bv []) #[a, n.asBV]

/--
info: mem_subset'.of_bv (a an b bn : BitVec 64) (h : a ‚â§ a + an ‚àß b ‚â§ b + bn ‚àß b ‚â§ a ‚àß a + an ‚â§ b + bn) :
  mem_subset' a an b bn
-/
#guard_msgs in #check mem_subset'.of_bv

instance : SolverReducible MemSubsetProp where
  reduceToSolver subset :=
  let a := subset.sa.base
  let an := subset.sa.n
  let b := subset.sb.base
  let bn := subset.sb.n
  mkAppN (Expr.const ``mem_subset'.of_bv []) #[a, an.asBV, b, bn.asBV]

/--
info: mem_separate'.of_bv (a an b bn : BitVec 64)
  (h : a ‚â§ a + an ‚àß b ‚â§ b + bn ‚àß (a + an ‚â§ b ‚à® a ‚â• b + bn ‚à® an = 0 ‚à® bn = 0)) : mem_separate' a an b bn
-/
#guard_msgs in #check mem_separate'.of_bv

instance : SolverReducible MemSeparateProp where
  reduceToSolver separate :=
    let a := separate.sa.base
    let an := separate.sa.n
    let b := separate.sb.base
    let bn := separate.sb.n
    mkAppN (Expr.const ``mem_separate'.of_bv []) #[a, an.asBV, b, bn.asBV]

/--
`SolverReducible` is a value whose type is `omegaFact ‚Üí desiredFact`.
An example is `mem_lega'.of_bv n a`, which has type:
  `(h : a.toNat + n ‚â§ 2 ^ 64) ‚Üí mem_legal' a n`.

@bollu: TODO: this can be generalized further, what we actually need is
  a way to convert `e : Œ±` into the `omegaToDesiredFactFnVal`.
-/
def proveWithSolver?  {Œ± : Type} [ToMessageData Œ±] [SolverReducible Œ±] (e : Œ±)
    (hyps : Array Hypothesis) : SimpMemM (Option (Proof Œ± e)) := do
  let proofFromSolverVal := (SolverReducible.reduceToSolver e)
  check proofFromSolverVal
  -- (h : a.toNat + n ‚â§ 2 ^ 64) ‚Üí mem_legal' a n
  let proofTy ‚Üê inferType (SolverReducible.reduceToSolver e)
  check proofTy
  -- trace[simp_mem.info] "partially applied: '{proofFromSolverVal} : {proofTy}'"
  let obligationTy ‚Üê do -- (h : a.toNat + n ‚â§ 2 ^ 64)
    match proofTy with
    | Expr.forallE _argName argTy _body _binderInfo => pure argTy
    | _ => throwError "expected '{proofTy}' to a ‚àÄ"
  -- trace[simp_mem.info] "obligation type '{obligationTy}'"
  let obligationVal ‚Üê mkFreshExprMVar (type? := obligationTy)
  check obligationVal
  let factProof := mkAppN proofFromSolverVal #[obligationVal]
  check factProof

  let mut goal := obligationVal.mvarId!

  SimpMemM.withTraceNode m!"{processingEmoji} `proveWithSolver?` obligation before 'mem_unfold_bv'" do
    trace[simp_mem.info] "{goal}"
  let oldGoals ‚Üê getGoals
  try
    setGoals [goal]
    let _ ‚Üê Hypothesis.addSolverFactsOfHyps goal hyps.toList #[]
    SimpMemM.withContext goal do
      withoutRecover do
        evalTactic (‚Üê `(tactic| mem_unfold_bv))
    let newGoals ‚Üê getGoals

    if newGoals.isEmpty then
      trace[simp_mem.info] "{checkEmoji} `proveWithSolver?` goal closed by `mem_unfold_bv`"
    else if newGoals.length > 1 then
      throwError "internal error: `mem_unfold_bv` produced more than one goal: {newGoals}"
    else
      let newGoal := newGoals[0]!
      SimpMemM.withTraceNode m!"`{processingEmoji} proveWithSolver?` goal before `bv_decide`" do
        trace[simp_mem.info] "{newGoal}"
      setGoals [newGoal]
      SimpMemM.withContext newGoal do
        evalTactic (‚Üê `(tactic| bv_decide))

      -- withoutRecover do
      --   evalTactic (‚Üê `(tactic| bv_decide))
  catch e =>
    trace[simp_mem.info]  "{crossEmoji} mem_decide_bv with error: \n{e.toMessageData}"
    setGoals oldGoals
    return .none

  if !(‚Üê Tactic.getUnsolvedGoals).isEmpty then
    throwError "internal error: bvDecide failed to solve unsolved goals: {(‚Üê Tactic.getUnsolvedGoals)}"

  setGoals oldGoals
  return (.some <| Proof.mk (‚Üê instantiateMVars factProof))
  end ReductionToOmega

section Simplify

def SimpMemM.rewriteWithEquality (rw : Expr) (msg : MessageData) : SimpMemM Unit := do
  withTraceNode msg do
     do
      withTraceNode m!"rewrite (Note: can be large)" do
        trace[simp_mem.info] "{‚Üê inferType rw}"
      let goal ‚Üê getMainGoal
      let result ‚Üê goal.rewrite (‚Üê getMainTarget) rw
      let mvarId' ‚Üê (‚Üê getMainGoal).replaceTargetEq result.eNew result.eqProof
      trace[simp_mem.info] "{checkEmoji} rewritten goal {mvarId'}"
      unless result.mvarIds == [] do
        throwError m!"{crossEmoji} internal error: expected rewrite to produce no side conditions. Produced {result.mvarIds}"
      replaceMainGoal [mvarId']

/--
info: Memory.read_bytes_write_bytes_eq_read_bytes_of_mem_separate' {x : BitVec 64} {xn : Nat} {y : BitVec 64} {yn : Nat}
  {mem : Memory} (hsep : mem_separate' x (‚Üëxn) y ‚Üëyn) (val : BitVec (yn * 8)) :
  Memory.read_bytes xn x (Memory.write_bytes yn y val mem) = Memory.read_bytes xn x mem
-/
#guard_msgs in #check Memory.read_bytes_write_bytes_eq_read_bytes_of_mem_separate'

/-- given that `e` is a read of the write, perform a rewrite,
using `Memory.read_bytes_write_bytes_eq_read_bytes_of_mem_separate'`. -/
def SimpMemM.rewriteReadOfSeparatedWrite
    (er : ReadBytesExpr) (ew : WriteBytesExpr)
    (separate : MemSeparateProof { sa := er.span, sb := ew.span }) : SimpMemM Unit := do
  let call :=
    mkAppN (Expr.const ``Memory.read_bytes_write_bytes_eq_read_bytes_of_mem_separate' [])
      #[er.span.base, er.span.n.asNat,
        ew.span.base, ew.span.n.asNat,
        ew.mem,
        separate.h,
        ew.val]
  SimpMemM.rewriteWithEquality call m!"rewriting read({er})‚üÇwrite({ew})"

/--
info: Memory.read_bytes_eq_extractLsBytes_sub_of_mem_subset' {a : BitVec 64} {an : Nat} {b : BitVec 64} {bn : Nat}
  {val : BitVec (bn * 8)} {mem : Memory} (hread : Memory.read_bytes bn b mem = val)
  (hsubset : mem_subset' a (‚Üëan) b ‚Üëbn := by mem_decide_bv) :
  Memory.read_bytes an a mem = val.extractLsBytes (a.toNat - b.toNat) an
-/
#guard_msgs in #check Memory.read_bytes_eq_extractLsBytes_sub_of_mem_subset'

def SimpMemM.rewriteReadOfSubsetRead
    (er : ReadBytesExpr) -- Memory.read_bytes a an mem
    (hread : ReadBytesEqProof) -- Memory.read_bytes bn b mem = val
    (hsubset : MemSubsetProof { sa := er.span, sb := hread.read.span })
  : SimpMemM Unit := do
  let a := er.span.base
  let an := er.span.n
  let b := hread.read.span.base
  let bn := hread.read.span.n
  let val := hread.val
  let call := mkAppN (Expr.const ``Memory.read_bytes_eq_extractLsBytes_sub_of_mem_subset' [])
    #[a, an.asNat,
      b, bn.asNat,
      val,
      er.mem,
      hread.h,
      hsubset.h]
  SimpMemM.rewriteWithEquality call m!"rewriting read({er})‚äÜread({hread.read})"

/--
info: Memory.read_bytes_write_bytes_eq_of_mem_subset' {x : BitVec 64} {xn : Nat} {y : BitVec 64} {yn : Nat} {mem : Memory}
  (hsep : mem_subset' x (‚Üëxn) y ‚Üëyn := by mem_decide_bv) (val : BitVec (yn * 8)) :
  Memory.read_bytes xn x (Memory.write_bytes yn y val mem) = val.extractLsBytes (x.toNat - y.toNat) xn
-/
#guard_msgs in #check Memory.read_bytes_write_bytes_eq_of_mem_subset'

def SimpMemM.rewriteReadOfSubsetWrite
    (er : ReadBytesExpr) -- Memory.read_bytes a an mem
    (ew : WriteBytesExpr) -- Memory.write_bytes b bn val mem
    (hsubset : MemSubsetProof { sa := er.span, sb := ew.span }) : SimpMemM Unit := do
  let a := er.span.base
  let an := er.span.n
  let b := ew.span.base
  let bn := ew.span.n
  let val := ew.val
  let mem := ew.mem
  let call := mkAppN (Expr.const ``Memory.read_bytes_write_bytes_eq_of_mem_subset' [])
    #[a, an.asNat,
      b, bn.asNat,
      mem,
      hsubset.h,
      val]
  SimpMemM.rewriteWithEquality call m!"rewriting read({er})‚äÜwrite({ew})"

mutual

/--
Pattern match for memory patterns, and simplify them.
Close memory side conditions with `simplifyGoal`.
Returns if progress was made.
-/
partial def SimpMemM.simplifyExpr (e : Expr) (hyps : Array Hypothesis) : SimpMemM Bool := do
  consumeRewriteFuel
  if ‚Üê outofRewriteFuel? then
    trace[simp_mem.info] "out of fuel for rewriting, stopping."
    return false

  if e.isSort then
    trace[simp_mem.info] "skipping sort '{e}'."
    return false

  if let .some er := ReadBytesExpr.ofExpr? e then
    if let .some ew := WriteBytesExpr.ofExpr? er.mem then
      trace[simp_mem.info] "{checkEmoji} Found read of write."
      trace[simp_mem.info] "read: {er}"
      trace[simp_mem.info] "write: {ew}"
      trace[simp_mem.info] "{processingEmoji} read({er.span})‚üÇ/‚äÜwrite({ew.span})"
      let subset := MemSubsetProp.mk er.span ew.span
      trace[simp_mem.info] "[1/2] {processingEmoji} {subset}"
      if let .some subsetProof ‚Üê proveWithSolver? subset hyps then do
        trace[simp_mem.info] "[1/2] {checkEmoji} {subset}"
        rewriteReadOfSubsetWrite er ew subsetProof
        return true
      else
        trace[simp_mem.info] "[1/2] {crossEmoji} {subset}"
        let separate := MemSeparateProp.mk er.span ew.span
        trace[simp_mem.info] "[2/2] {processingEmoji} {separate}"
        if let .some separateProof ‚Üê proveWithSolver? separate hyps then do
          trace[simp_mem.info] "[2/2] {checkEmoji} {separate}"
          rewriteReadOfSeparatedWrite er ew separateProof
          return true
        else
          trace[simp_mem.info] "[2/2] {crossEmoji} {separate}"
          trace[simp_mem.info] "{crossEmoji} Could not prove {er.span} ‚üÇ/‚äÜ {ew.span}"
          return false
    else
      -- read
      trace[simp_mem.info] "{checkEmoji} Found read {er}."
      -- TODO: we don't need a separate `subset` branch for the writes: instead, for the write,
      -- we can add the theorem that `(write region).read = write val`.
      -- Then this generic theory will take care of it.
      let changedInCurrentIter? ‚Üê withTraceNode m!"Searching for overlapping read {er.span}." do
        let mut changedInCurrentIter? := false
        for hyp in hyps do
          if let Hypothesis.read_eq hReadEq := hyp then do
            -- the read we are analyzing should be a subset of the hypothesis
            let subset := (MemSubsetProp.mk er.span hReadEq.read.span)
            changedInCurrentIter? := changedInCurrentIter? ||
              (‚Üê withTraceNode m!"{processingEmoji} {subset} ? " do
                if let some hSubsetProof ‚Üê proveWithSolver? subset hyps then
                  trace[simp_mem.info] "{checkEmoji}  {subset}"
                  rewriteReadOfSubsetRead er hReadEq hSubsetProof
                  pure true
                else
                  trace[simp_mem.info] "{crossEmoji}  {subset}"
                  pure false)
        pure changedInCurrentIter?
      return changedInCurrentIter?
  else
    if e.isForall then
      Lean.Meta.forallTelescope e fun xs b => do
        let mut changedInCurrentIter? := false
        for x in xs do
          changedInCurrentIter? := changedInCurrentIter? || (‚Üê SimpMemM.simplifyExpr x hyps)
          -- we may have a hypothesis like
          -- ‚àÄ (x : read_mem (read_mem_bytes ...) ... = out).
          -- we want to simplify the *type* of x.
          changedInCurrentIter? := changedInCurrentIter? || (‚Üê SimpMemM.simplifyExpr (‚Üê inferType x) hyps)
        trace[simp_mem.info] "{processingEmoji} Simplifying body of ‚àÄ {b}"
        changedInCurrentIter? := changedInCurrentIter? || (‚Üê SimpMemM.simplifyExpr b hyps)
        return changedInCurrentIter?
    else if e.isLambda then
      Lean.Meta.lambdaTelescope e fun xs b => do
        let mut changedInCurrentIter? := false
        for x in xs do
          changedInCurrentIter? := changedInCurrentIter? || (‚Üê SimpMemM.simplifyExpr x hyps)
          changedInCurrentIter? := changedInCurrentIter? || (‚Üê SimpMemM.simplifyExpr (‚Üê inferType x) hyps)
        changedInCurrentIter? := changedInCurrentIter? || (‚Üê SimpMemM.simplifyExpr b hyps)
        return changedInCurrentIter?
    else
      -- check if we have expressions.
      match e with
      | .app f x =>
        let mut changedInCurrentIter? := false
        changedInCurrentIter? := changedInCurrentIter? || (‚Üê SimpMemM.simplifyExpr f hyps)
        changedInCurrentIter? := changedInCurrentIter? || (‚Üê SimpMemM.simplifyExpr x hyps)
        return changedInCurrentIter?
      | _ =>
        return false


/--
simplify the goal state, closing legality, subset, and separation goals,
and simplifying all other expressions. return `true` if goal has been closed, and `false` otherwise.
-/
partial def SimpMemM.closeGoal (g : MVarId) (hyps : Array Hypothesis) : SimpMemM Bool := do
  SimpMemM.withContext g do
    trace[simp_mem.info] "{processingEmoji} Matching on ‚ä¢ {‚Üê g.getType}"
    let gt ‚Üê g.getType
    if let .some e := MemLegalProp.ofExpr? gt then
      withTraceNode m!"Matched on ‚ä¢ {e}. Proving..." do
        if let .some proof ‚Üê proveWithSolver? e hyps then
          g.assign proof.h
    if let .some e := MemSubsetProp.ofExpr? gt then
      withTraceNode m!"Matched on ‚ä¢ {e}. Proving..." do
        if let .some proof ‚Üê proveWithSolver? e hyps then
          g.assign proof.h
    if let .some e := MemSeparateProp.ofExpr? gt then
      withTraceNode m!"Matched on ‚ä¢ {e}. Proving..." do
        if let .some proof ‚Üê proveWithSolver? e hyps then
          g.assign proof.h
  return ‚Üê g.isAssigned



/--
simplify the goal state, closing legality, subset, and separation goals,
and simplifying all other expressions. Returns `true` if an improvement has been made
in the current iteration.
-/
partial def SimpMemM.simplifyGoal (g : MVarId) (hyps : Array Hypothesis) : SimpMemM Bool := do
  SimpMemM.withContext g do
    let gt ‚Üê g.getType
    let changedInCurrentIter? ‚Üê withTraceNode m!"Simplifying goal." do
        SimpMemM.simplifyExpr (‚Üê whnf gt) hyps
    return changedInCurrentIter?
end

/--
The core simplification loop.
We look for appropriate hypotheses, and simplify (often closing) the main goal using them.
-/
partial def SimpMemM.simplifyLoop : SimpMemM Unit := do
  let g ‚Üê getMainGoal
  g.withContext do
    let hyps := (‚Üê getLocalHyps)
    let foundHyps ‚Üê withTraceNode m!"Searching for Hypotheses" do
      let mut foundHyps : Array Hypothesis := #[]
      for h in hyps do
        foundHyps ‚Üê hypothesisOfExpr h foundHyps
      pure foundHyps

    withTraceNode m!"Summary: Found {foundHyps.size} hypotheses" do
      for (i, h) in foundHyps.toList.enum do
        trace[simp_mem.info] m!"{i+1}) {h}"

    if ‚Üê SimpMemM.closeGoal g foundHyps then
      trace[simp_mem.info] "{checkEmoji} goal closed."
      return ()

    -- goal was not closed, try and improve.
    let mut changedInAnyIter? := false
    while true do
      if (‚Üê outofRewriteFuel?) then break

      let changedInCurrentIter? ‚Üê withTraceNode m!"Performing Rewrite At Main Goal" do
        SimpMemM.simplifyGoal (‚Üê getMainGoal) foundHyps
      changedInAnyIter? := changedInAnyIter? || changedInCurrentIter?

      if !changedInCurrentIter? then
        trace[simp_mem.info] "{crossEmoji} No progress made in this iteration. halting."
        break

    /- we haven't changed ever.. -/
    if !changedInAnyIter? && (‚Üê getConfig).failIfUnchanged then
        throwError "{crossEmoji} simp_mem failed to make any progress."
end Simplify

/--
Given a collection of facts, try prove `False` using the omega algorithm,
and close the goal using that.
-/
def simpMem (cfg : SimpMemConfig := {}) : TacticM Unit := do
  -- evalTactic (‚Üê `(simp (config := {failIfUnchanged := false}) only [memory_rules]))
  SimpMemM.run SimpMemM.simplifyLoop cfg


/-- The `simp_mem` tactic, for simplifying away statements about memory. -/
def simpMemTactic (cfg : SimpMemConfig) : TacticM Unit := simpMem cfg

def simpMemDebugTactic  : TacticM Unit := do
  SimpMemM.run (cfg := {}) do
    SimpMemM.withMainContext do
    -- evaluate mem_decide_bv
    return ()

end SeparateAutomation

/--
Allow elaboration of `SimpMemConfig` arguments to tactics.
-/
declare_config_elab elabSimpMemConfig SeparateAutomation.SimpMemConfig

/--
Implement the simp_mem tactic frontend.
-/
syntax (name := simp_mem) "simp_mem" (Lean.Parser.Tactic.config)? : tactic

@[tactic simp_mem]
def evalSimpMem : Tactic := fun
  | `(tactic| simp_mem $[$cfg]?) => do
    let cfg ‚Üê elabSimpMemConfig (mkOptionalNode cfg)
    SeparateAutomation.simpMemTactic cfg
  | _ => throwUnsupportedSyntax



namespace SeparateAutomation.Lint

/-- Return `some (n, type)` if `e` is an `OfNat.ofNat`-application encoding `n` for a type with name `typeDeclName`. -/
def getOfNatExpr? (e : Expr) (typeDeclName : Name) : MetaM (Option (Expr √ó Expr)) := OptionT.run do
  let_expr OfNat.ofNat type n _ ‚Üê e | failure
  let type ‚Üê whnfD type
  guard <| type.getAppFn.isConstOf typeDeclName
  return (n, type)

/-- Return `some n` if `e` is a raw natural number or an `OfNat.ofNat`-application encoding `n`. -/
def getNatExpr? (e : Expr) : MetaM (Option Expr) := do
  let e := e.consumeMData
  if let some n := getRawNatValue? e then
    return some e
  let some (n, _) ‚Üê getOfNatExpr? e ``Nat | return none
  return some n

/-
return the (width, value) if the expression is some kind of bitvec literal,
loosely understood.
-/
def getBitVecExpr? (e : Expr) : MetaM (Option (Expr √ó Expr)) := do
  match_expr e with
  | BitVec.ofNat nExpr vExpr => return .some (nExpr, vExpr)
  | BitVec.ofNatLt nExpr vExpr _ =>return (nExpr, vExpr)
  | _ =>
    let .some (v, type) ‚Üê getOfNatExpr? e ``BitVec
      | return none
    let n := type.appArg!
    return .some ‚ü®n, v‚ü©

/-- Warning string guarding users.-/
def warningStr : String := "Here be dragons üíÄ."

-- IF we have a function application, of a function whose arguments aren't `Nat`,
-- then it's likely that it's the user extracting data from a field or something.
-- On the other hand, if the function is applied to a `Nat`, then it's likely that
-- the user is manipulating natural numbers, and we want to prevent this.
def lintBitVecComplexValue (parent : Expr) (e : Expr) : TacticM Unit := do
  if !e.isApp then
    logWarning m!"{parent} has a non-simple-function-application to a bitvector value. {warningStr}"
  else
    let (name, xs) := e.getAppFnArgs
    if name.isAnonymous then
      logWarning m!"{parent} has a call of a non-constant function. {warningStr}"
    else do
      let decl ‚Üê getFunInfo e.getAppFn
      for (x, param) in xs.zip decl.paramInfo do
        if param.isImplicit then continue
        if (‚Üê inferType x) == mkConst ``Nat then
          logWarning m!"{parent} has a call of '{name}' with a `Nat` argument. {warningStr}"
          return ()

partial def lintCore (e : Expr) : TacticM Unit := do
  if let .some (w, v) ‚Üê liftMetaM <| getBitVecExpr? e then
    match ‚Üê getNatExpr? w with
    | .some _ => pure ()
    | .none => logWarning m!"bitvector with symbolic width: {e}. Please make widths constant for bitblasting."

    if v.isAppOf ``BitVec.toNat then
      logWarning m!"'{e}' has a bitvector value being converted to a Nat. {warningStr}"

    -- | TODO: what we should actually do is to check if this contains arithmetic expressions.
    -- For example, something like `SHA512.length` is fine, but `SHA512.length + 1` is probably
    -- not what the user intended.
    if v.isFVar || (‚Üê getNatValue? v).isSome then
      pure ()
    else
      lintBitVecComplexValue e v
  else
    let t ‚Üê whnf (‚Üê inferType e)
    -- `e` is not a bitvector expression.
    if !t.isAppOf ``BitVec then return ()
    -- `e` is a `Nat.cast`, so they are converting a `Nat` to a `BitVec`.
    -- they better know what they're doing.
    if e.isAppOf ``Nat.cast then
      logWarning m!"'{e}' casting a bitvector to a Nat. {warningStr}"


def lintTactlc : TacticM Unit := do
  withMainContext do
    let hyps := (‚Üê getLocalHyps)
    for hyp in hyps do
      let t ‚Üê inferType hyp
      Meta.forEachExpr t lintCore
    -- lint the goal.
    let t ‚Üê getMainTarget
    Meta.forEachExpr t lintCore

end SeparateAutomation.Lint

/--
Lint the current hypotheses and goal state, to make sure we don't have
dodgy occurrences of `(BitVec.ofNat w)` where `w` is symbolic, and we also
don't have `(BitVec.ofNat 64 <expression>)` where `<expression>` is not a constant,
nor is a variable. So expressions such as:
- `BitVec.ofNat 64 100`
- `BitVec.ofNat 64 num_blocks.`

are permissible, but expressions such as:
- `BitVec.ofNat 64 (SHA_table_length / 16)`
- `BitVec.ofNat 64 (num_blocks * 8)`

are not.
-/
syntax (name := simp_mem_lint) "simp_mem_lint" : tactic

@[tactic simp_mem_lint]
def evalSimpMemLint : Tactic := fun
  | `(tactic| simp_mem_lint) => do
    SeparateAutomation.Lint.lintTactlc
  | _ => throwUnsupportedSyntax

/--
Prove that the read from a write is a separated read.
-/
syntax (name := simp_mem_separate) "simp_mem_separate" : tactic

/--
-- Prove that the read from a write is a subset read.
-/
syntax (name := simp_mem_subset) "simp_mem_subset" : tactic

/--
Prove that the read from a write is a read from known memory location.
-/
syntax (name := simp_mem_read) "simp_mem_read" : tactic

/--
Implement the simp_mem tactic frontend.
-/
syntax (name := simp_mem_debug) "simp_mem_debug" :  tactic
